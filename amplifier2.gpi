set terminal pngcairo enhanced size 1080,960 lw 1.5
set output 'plots/amplifier2.png'
set angle degrees

I = {0,1}
Vcc = 10
Vbb = 2
Vbe = 0.6
Rc = 400
Rb = 20e3
Rl = 1e3
Co = 100e-6
beta = 180

H(x) = - beta * Rc * Rl / (Rb * (Rl + Rc)) \
* 2 * pi * x / (2 * pi * x + 1/(Co * (Rc + Rl)))

db(x) = 20 * log10(abs(x))
ph(x) = 180 / pi * atan(x)
set multiplot layout 2,2 title 'Common emitter amplifier DC filtering'


set grid lt 0
set format x "%.0s"
set xrange [0:0.035]
set yrange [-5:5]
set xlabel 'time (ms)'
set xzeroaxis lt -1

set title 'Voltages vs time (Ngspice Simulation)'
set ylabel 'Voltage (V)'
set key above
set key inside
plot 'results/voltages.txt' u 1:2 with lines title 'v_{in}', \
     'results/voltages.txt' u 1:4 with lines lt 3 title 'v_{out}_{(sat)}', \
     'results/voltages.txt' u 1:5 with lines lt 4 title 'v_{out}_{(cut)}', \
     'results/voltages.txt' u 1:3 with lines lt 2 dt '--' title 'v_{out}', \

set title 'AC Magnitude Response'
set key bottom right
set format x '%.0s %c'
set yrange [-60:20]
unset xrange
set logscale x
set ylabel '20 log_{10}|H_{(j w)}|'
set xlabel 'Frequency (Hz)'
set grid lt -1
plot 'results/voltages_ac.txt' u 1:2 w lines lc 2 title 'Simulation', \
     db(H(I*x)) w lines lc 1 dt '--' title 'Formula'

set title 'Transistor regions'
set key default
set xlabel 'i_b (A)'
set ylabel 'V_{CE} (V)'
unset format y
set key right
set logscale x
set grid lt -1
unset xrange
unset yrange
plot 'results/transistor.txt' u 2:3 lc 2 w lines title 'active', \
     'results/transistor.txt' u 4:5 lc 3 w lines title 'sat', \
     'results/transistor.txt' u 6:7 lc 4 w lines title 'cut'

set title 'AC Phase Response'
set format y '%.0s^o'
unset yrange
set ylabel 'tan^{-1} H_{(j w)}'
set logscale x
plot 'results/voltages_ac.txt' u 1:3 w lines lc 2 title 'Simulation', \
     arg(H(I*x)) lc 1 dt '--' title 'formula'

