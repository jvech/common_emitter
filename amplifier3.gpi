set terminal pngcairo enhanced size 1080,920 lw 1.5
set output 'plots/amplifier3.png'

I = {0,1}
r1 = 40e3
r2 = 10e3
ci = 10e-6
co = 10e-6
rc = 3.5e3
re = 1e3
rl = 10e3
beta = 180
r_e = 52


zth(x) = r1 * r2 / (r1 * r2 * ci * x + r1 + r2)
f(x) = - beta * \
rc * rl * co * x / ((rl + rc) * co * x + 1) * \
ci * zth(x) * x / (zth(x) + (beta + 1) * (re + r_e))

db(x) = 20 * log10(abs(x))
ph(x) = 180 / pi * atan(f(x))
set multiplot layout 2,2 title 'Common emitter amplifier DC filtering'


set grid lt 0
set format x "%.0s"
set xrange [0:0.035]
#set yrange [-5:5]
set xlabel 'time (ms)'
set xzeroaxis lt -1

set title 'Voltages vs time (Ngspice Simulation)'
set ylabel 'Voltage (V)'
set key above
set key inside
plot 'results/voltages.txt' u 1:2 with lines title 'v_{in}', \
     'results/voltages.txt' u 1:3 with lines title 'v_{out}', \


set title 'Transistor regions'
set xlabel 'base \& collector currents (A)'
set ylabel 'V_{CE} (V)'
unset format y
set key default
set logscale x
set grid lt -1
unset xrange
unset yrange
plot 'results/transistor.txt' u 3:2 title 'i_b',\
     'results/transistor.txt' u 4:2 title 'i_c'


set title 'AC Magnitude Response'
set key bottom right
unset format y
set format x '%.0s %c'
#set yrange [-60:20]
unset xrange
set logscale x
set ylabel '20 log_{10}|H_{(j w)}|'
set xlabel 'Frequency (Hz)'
set grid lt -1
plot 'results/voltages_ac.txt' u 1:2 w lines lc 2 title 'Simulation', \
     'results/voltages_ac.txt' u 1:(db(f(2 * pi * $1 * I))) w lines dt '--' lc 1 title 'Formula'


set title 'AC Phase Response'
set format y '%.0s^o'
set key default
unset yrange
set ylabel 'tan^{-1} H_{(j w)}'
set logscale x
set angle degrees
plot 'results/voltages_ac.txt' u 1:3 w lines title 'simulation' lc 2, \
     'results/voltages_ac.txt' u 1:(arg(f(I * 2 * pi * $1))) w lines lc 1 dt '--' title 'formula'
