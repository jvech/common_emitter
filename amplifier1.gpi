set terminal pngcairo enhanced size 1080,960 lw 1.5
set output 'plots/amplifier1.png'

f(x, Vcc, Vbb, Vbe, Rc, Rb, Rl, beta) = \
(Rl * Rb * Vcc + beta * Rc * Rl * (Vbe - Vbb)) / (Rb * (Rl + Rc)) \
- beta * Rc * Rl * x / (Rb * (Rl + Rc))



scale(x, base) = x * base

set multiplot layout 2,2 title 'Common emitter amplifier simplest setup'
#unset border

set title 'Voltages vs time (ngspice Simulation)'
set grid lt -1
set format x "%.0s"
set xrange [0:0.035]
set yrange [-1.5:12]
set xlabel 'time (ms)'
set xzeroaxis lt -1
set ylabel 'Voltage (V)'

plot 'results/voltages.txt' u 1:2 with lines dt 4  title 'v_{in}', \
     'results/voltages.txt' u 1:3 with lines title 'v_{out}', \
     'results/voltages.txt' u 1:4 with lines title 'v_{out}_{(sat)}', \
     'results/voltages.txt' u 1:5 with lines title 'v_{out}_{(cut)}'

set format y '%.1s %c'
set title 'Voltage vs time (formula)' enhanced
#set yrange [100:200]
plot 'results/voltages.txt' u 1:2 w lines title 'v_{in}', \
     'results/voltages.txt' u 1:(f($2, 10, 2, 0.6, 400, 20e3, 1e3, 180)) with lines title 'v_{out}'


set format x "%.1s %c"
set xlabel 'v_{in}'
set ylabel 'V_{out}'
set yrange [-1:8]
unset xrange
set xtic rotate by 45 right
set title 'Input vs Output'
plot 'results/voltages.txt' u 2:3 w lines lc 2 title 'v_{out}', \
     'results/voltages.txt' u 2:4 w lines lc 3 title 'v_{out}_{(sat)}', \
     'results/voltages.txt' u 2:5 w lines lc 4 title 'v_{out}_{(cut)}'

set xlabel 'i_b (A)'
set ylabel 'V_{CE} (V)'

set key right
set logscale x
set title 'Transistor regions'
plot 'results/transistor.txt' u 2:3 lc 2 w lines title 'active', \
     'results/transistor.txt' u 4:5 lc 3 w lines title 'sat', \
     'results/transistor.txt' u 6:7 lc 4 w lines title 'cut'
